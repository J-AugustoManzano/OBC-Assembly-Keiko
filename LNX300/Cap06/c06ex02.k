MODULE c06ex02 0xFFFFFFFF 0
IMPORT In  0x5d1e568e
IMPORT Out 0x66adaa46
ENDHDR

PROC c06ex02.%main 0 0 0

! Escreva "--- Matriz 4x3 (Reais entre 0 e 99) ---"
CONST 40
GLOBAL c06ex02.%1
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

! Salta linha
GLOBAL Out.Ln
CALL 0

! Para i de 0 Até 3 Faça
CONST 0
STGW c06ex02.i
LABEL INICIO_LINHAS_ENTRADA
LDGW c06ex02.i
CONST 3
JGT FIM_LINHAS_ENTRADA

! Para j de o Até 2 Faça
CONST 0
STGW c06ex02.j
LABEL INICIO_COLUNAS_ENTRADA
LDGW c06ex02.j
CONST 2
JGT FIM_COLUNAS_ENTRADA

LABEL TENTA_NOVO_VALOR

! Escreva "Entre A[" .. i + 1
CONST 9
GLOBAL c06ex02.%2
GLOBAL Out.String
CALL 2
CONST 0
LDGW c06ex02.i
INC
GLOBAL Out.Int
CALL 2

! Escreva "," .. j + 1 .. "] --> "
CONST 2
GLOBAL c06ex02.%9
GLOBAL Out.String
CALL 2
CONST 0
LDGW c06ex02.j
INC
GLOBAL Out.Int
CALL 2
CONST 7
GLOBAL c06ex02.%3
GLOBAL Out.String
CALL 2

! Leia a[i, j]
GLOBAL c06ex02.a
LDGW c06ex02.i
CONST 4
BOUND 0
CONST 3
TIMES
LDGW c06ex02.j
CONST 3
BOUND 0
PLUS
INDEXW
GLOBAL In.Real
CALL 1

! Até que (a[i, j] >= 0.0) .e. (a[i, j] <= 99.0)
GLOBAL c06ex02.a
LDGW c06ex02.i
CONST 4
BOUND 0
CONST 3
TIMES
LDGW c06ex02.j
CONST 3
BOUND 0
PLUS
LDIF
FCONST 0.0
FJNGEQ TENTA_NOVO_VALOR
GLOBAL c06ex02.a
LDGW c06ex02.i
CONST 4
BOUND 0
CONST 3
TIMES
LDGW c06ex02.j
CONST 3
BOUND 0
PLUS
LDIF
FCONST 99.0
FJNLEQ TENTA_NOVO_VALOR

! Incrementa j de 1 em 1
LDGW c06ex02.j
INC
STGW c06ex02.j
JUMP INICIO_COLUNAS_ENTRADA
LABEL FIM_COLUNAS_ENTRADA

! Incrementa i de 1 em 1
LDGW c06ex02.i
INC
STGW c06ex02.i
JUMP INICIO_LINHAS_ENTRADA
LABEL FIM_LINHAS_ENTRADA

! Salta linha
GLOBAL Out.Ln
CALL 0

! Escreva "--- Valores Informados ---"
CONST 27
GLOBAL c06ex02.%4
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

! Salta linha
GLOBAL Out.Ln
CALL 0

! Escreva "-----------------------------")
CONST 30
GLOBAL c06ex02.%5
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

! Escreva "| A |   1   |   2   |   3   |"
CONST 30
GLOBAL c06ex02.%6
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

! Escreva "-----------------------------")
CONST 30
GLOBAL c06ex02.%5
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

! Para i de 0 Até 3 Faça
CONST 0
STGW c06ex02.i
LABEL INICIO_LINHAS_SAIDA
LDGW c06ex02.i
CONST 3
JGT FIM_LINHAS_SAIDA

! Escreva "| " .. i + 1 .. " |"
CONST 3
GLOBAL c06ex02.%7
GLOBAL Out.String
CALL 2
CONST 1
LDGW c06ex02.i
INC
GLOBAL Out.Int
CALL 2
CONST 3
GLOBAL c06ex02.%8
GLOBAL Out.String
CALL 2

! Para j de o Até 2 Faça
CONST 0
STGW c06ex02.j
LABEL INICIO_COLUNAS_SAIDA
LDGW c06ex02.j
CONST 2
JGT FIM_COLUNAS_SAIDA

! Escreva [i, j]
CONST 1
CONST 6
GLOBAL c06ex02.a
LDGW c06ex02.i
CONST 4
BOUND 0
CONST 3
TIMES
LDGW c06ex02.j
CONST 3
BOUND 0
PLUS
LDIF
CONVFD
GLOBAL Out.Fixed
CALL 4

! Escreva " |"
CONST 3
GLOBAL c06ex02.%8
GLOBAL Out.String
CALL 2

! Incrementa j de 1 em 1
LDGW c06ex02.j
INC
STGW c06ex02.j
JUMP INICIO_COLUNAS_SAIDA
LABEL FIM_COLUNAS_SAIDA

! Salta linha
GLOBAL Out.Ln
CALL 0

! Escreva "-----------------------------"
CONST 30
GLOBAL c06ex02.%5
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

! Incrementa i de 1 em 1
LDGW c06ex02.i
INC
STGW c06ex02.i
JUMP INICIO_LINHAS_SAIDA
LABEL FIM_LINHAS_SAIDA

RETURN
END

! Variáveis globais
GLOVAR c06ex02.a 48
GLOVAR c06ex02.i 4
GLOVAR c06ex02.j 4

! Texto "--- Matriz 4x3 ---"
DEFINE c06ex02.%1
STRING 2D2D2D204D617472697A20347833202D2D2D00

! Texto "Entre A["
DEFINE c06ex02.%2
STRING 456E74726520415B00

! Texto "] --> "
DEFINE c06ex02.%3
STRING 5D202D2D3E2000

! Texto "--- Valores Informados ---"
DEFINE c06ex02.%4
STRING 2D2D2D2056616C6F72657320496E666F726D61646F73202D2D2D00

! Texto "-----------------------------"
DEFINE c06ex02.%5
STRING 2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D00

! Texto "| A |   1   |   2   |   3   |"
DEFINE c06ex02.%6
STRING 7C2041207C202020312020207C202020322020207C202020332020207C00

! Texto "| "
DEFINE c06ex02.%7
STRING 7C2000

! Texto " |"
DEFINE c06ex02.%8
STRING 207C00

! Texto ","
DEFINE c06ex02.%9
STRING 2C00
