MODULE c06ex04 0xFFFFFFFF 0
IMPORT In  0x1d6e8955
IMPORT Out 0x190c339a
ENDHDR

PROC c06ex04.%main 0 4 0

! Para i de 0 Até MAXPESSOAS - 1 Faça
CONST 0
STGW c06ex04.i
LABEL INICIO_CADASTRO_PESSOAS
LDGW c06ex04.i
CONST 4
JGT FIM_CADASTRO_PESSOAS

! Escreva "--- Pessoa " .. (i + 1) .. " ---" 
CONST 12
GLOBAL c06ex04.%1
GLOBAL Out.String
CALL 2
CONST 0
LDGW c06ex04.i
INC
GLOBAL Out.Int
CALL 2
CONST 5
GLOBAL c06ex04.%2
GLOBAL Out.String
CALL 2

! Salta linha
GLOBAL Out.Ln
CALL 0

! Escreva "Entre o nome ....: "
CONST 20
GLOBAL c06ex04.%3
GLOBAL Out.String
CALL 2

! Escreva pes[i].nome
GLOBAL c06ex04.pes
LDGW c06ex04.i
CONST 5
BOUND 0
CONST 44
TIMES
OFFSET
CONST 40
SWAP
GLOBAL In.Line
CALL 2

! Escreva "Entre a idade ...: "
CONST 20
GLOBAL c06ex04.%4
GLOBAL Out.String
CALL 2

! Escreva pes[i].idade
GLOBAL c06ex04.pes
LDGW c06ex04.i
CONST 5
BOUND 0
CONST 44
TIMES
OFFSET
CONST 40
OFFSET
GLOBAL In.Int
CALL 1

! Salta linha;
GLOBAL Out.Ln
CALL 0

! Para i de 0 Até MAXPESSOAS - 1 Faça
LDGW c06ex04.i
INC
STGW c06ex04.i
JUMP INICIO_CADASTRO_PESSOAS
LABEL FIM_CADASTRO_PESSOAS

! Salta linha;
GLOBAL Out.Ln
CALL 0

! Escreva "--- Pessoas Cadastradas ---"
CONST 28
GLOBAL c06ex04.%5
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

! Excreva "---------------------------"
CONST 28
GLOBAL c06ex04.%6
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

! Salta linha
GLOBAL Out.Ln
CALL 0

! Escreva " Posicao | Nome"
CONST 16
GLOBAL c06ex04.%7
GLOBAL Out.String
CALL 2

! Para i de 0 Até COLUNAS - 4 Faça
CONST 0
STGW c06ex04.i
LABEL INICIO_IMPRESSAO_ESPACO_TITULO
LDGW c06ex04.i
CONST 36
JGT FIM_IMPRESSAO_ESPACO_TITULO

! Escreva " "
CONST 32
ALIGNC
GLOBAL Out.Char
CALL 1

! Para i de 0 Até COLUNAS - 4 Faça
LDGW c06ex04.i
INC
STGW c06ex04.i
JUMP INICIO_IMPRESSAO_ESPACO_TITULO
LABEL FIM_IMPRESSAO_ESPACO_TITULO

! Escreva " | Idade"
CONST 9
GLOBAL c06ex04.%8
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

! Escreva "---------|"
CONST 11
GLOBAL c06ex04.%9
GLOBAL Out.String
CALL 2

! Para i := 0 Até COLUNAS + 2 Faça
CONST 0
STGW c06ex04.i
LABEL INICIO_BARRA_SUPERIOR
LDGW c06ex04.i
CONST 42
JGT FIM_BARRA_SUPERIOR

! Escreva "-"
LINE 47
CONST 45
ALIGNC
GLOBAL Out.Char
CALL 1

! Para i de 0 Até COLUNAS + 2 Faça
LDGW c06ex04.i
INC
STGW c06ex04.i
JUMP INICIO_BARRA_SUPERIOR
LABEL FIM_BARRA_SUPERIOR

! Escreva "|--------"
CONST 10
GLOBAL c06ex04.%10
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

! Para i de 0 Até MAXPESSOAS - 1 Faça
CONST 0
STGW c06ex04.i
LABEL INICIO_LISTAGEM
LDGW c06ex04.i
CONST 4
JGT FIM_LISTAGEM

! Escreva (i + 1) .. " | "
CONST 8
LDGW c06ex04.i
INC
GLOBAL Out.Int
CALL 2
CONST 4
GLOBAL c06ex04.%11
GLOBAL Out.String
CALL 2

! j := 0
CONST 0
STGW c06ex04.j
LABEL INICIO_ESCREVE_NOME

! Enquanto (j < COLUNAS) .E. (j < tamanho(pes[i].nome)) .E. 
!          (pes[i].nome[j] <> 0X) Faça
LDGW c06ex04.j
CONST 40
JGEQ FIM_NOME
LDGW c06ex04.j
GLOBAL c06ex04.pes
LDGW c06ex04.i
CONST 5
BOUND 0
CONST 44
TIMES
OFFSET
POP 1
CONST 40
JGEQ FIM_NOME
GLOBAL c06ex04.pes
LDGW c06ex04.i
CONST 5
BOUND 0
CONST 44
TIMES
OFFSET
LDGW c06ex04.j
CONST 40
BOUND 0
LDIC
JEQZ FIM_NOME

! Escreva pes[i].nome[j]
GLOBAL c06ex04.pes
LDGW c06ex04.i
CONST 5
BOUND 0
CONST 44
TIMES
OFFSET
LDGW c06ex04.j
CONST 40
BOUND 0
LDIC
ALIGNC
GLOBAL Out.Char
CALL 1

!  i := i + 1
LDGW c06ex04.j
INC
STGW c06ex04.j
JUMP INICIO_ESCREVE_NOME
LABEL FIM_NOME

! Enquanto (j < COLUNAS) Faça
LDGW c06ex04.j
CONST 40
JGEQ FIM_COMPLETA_ESPACOS

! Escreva " "
CONST 32
ALIGNC
GLOBAL Out.Char
CALL 1

! j := j + 1
LDGW c06ex04.j
INC
STGW c06ex04.j
JUMP FIM_NOME
LABEL FIM_COMPLETA_ESPACOS

! Escreva "  | "
CONST 5
GLOBAL c06ex04.%12
GLOBAL Out.String
CALL 2

! Escreva pes[i].idade, 5)
CONST 5
GLOBAL c06ex04.pes
LDGW c06ex04.i
CONST 5
BOUND 0
CONST 44
TIMES
OFFSET
LDNW 40
GLOBAL Out.Int
CALL 2
GLOBAL Out.Ln
CALL 0

! Para i de 0 até MAXPESSOAS - 1 Faça
LDGW c06ex04.i
INC
STGW c06ex04.i
JUMP INICIO_LISTAGEM
LABEL FIM_LISTAGEM

! Escreva "---------|"
CONST 11
GLOBAL c06ex04.%9
GLOBAL Out.String
CALL 2
CONST 0
STGW c06ex04.i
LABEL INICIO_BARRA_INFERIOR
LDGW c06ex04.i
CONST 42
JGT FIM_BARRA_INFERIOR

! Escreva "-"
CONST 45
ALIGNC
GLOBAL Out.Char
CALL 1
LDGW c06ex04.i
INC
STGW c06ex04.i
JUMP INICIO_BARRA_INFERIOR
LABEL FIM_BARRA_INFERIOR

! Escreva "|--------"
CONST 10
GLOBAL c06ex04.%10
GLOBAL Out.String
CALL 2
GLOBAL Out.Ln
CALL 0

RETURN
END

! Variáveis globais
GLOVAR c06ex04.pes 220
GLOVAR c06ex04.i 4
GLOVAR c06ex04.j 4

! Texto "--- Pessoa "
DEFINE c06ex04.%1
STRING 2D2D2D20506573736F612000

! Texto " ---"
DEFINE c06ex04.%2
STRING 202D2D2D00

! Texto "Entre o nome ....: "
DEFINE c06ex04.%3
STRING 456E747265206F206E6F6D65202E2E2E2E3A2000

! Texto "Entre a idade ...: "
DEFINE c06ex04.%4
STRING 456E7472652061206964616465202E2E2E3A2000

! Texto "--- Pessoas Cadastradas ---"
DEFINE c06ex04.%5
STRING 2D2D2D20506573736F6173204361646173747261646173202D2D2D00

! Texto "---------------------------"
DEFINE c06ex04.%6
STRING 2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D2D00

! Texto " Posicao | Nome"
DEFINE c06ex04.%7
STRING 20506F736963616F207C204E6F6D6500

! Texto " | Idade"
DEFINE c06ex04.%8
STRING 207C20496461646500

! Texto "---------|"
DEFINE c06ex04.%9
STRING 2D2D2D2D2D2D2D2D2D7C00

! Texto "|--------"
DEFINE c06ex04.%10
STRING 7C2D2D2D2D2D2D2D2D00

! Texto " | "
DEFINE c06ex04.%11
STRING 207C2000

! Texto "  | "
DEFINE c06ex04.%12
STRING 20207C2000
